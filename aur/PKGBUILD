# Maintainer: Francesc d'Ass√≠s Requesens i Roca <francescrequesens.com>

pkgname=api-calls-bin
pkgver=%PKGVER%
pkgrel=1
pkgdesc="A lightweight Electron-based desktop tool for sending and inspecting REST API requests."
arch=('x86_64')
url="https://github.com/elpeix/apicalls"
license=('GPL-3.0-only')
makedepends=('libarchive')
depends=(
  'gtk3'
  'nss'
  'nspr'
  'alsa-lib'
  'at-spi2-core'
  'libxcomposite'
  'libxdamage'
  'libxrandr'
  'libxkbcommon'
  'libxss'
  'libxtst'
  'mesa'
  'libdrm'
  'xdg-utils'
  'hicolor-icon-theme'
  'libnotify'
)
provides=('api-calls')
conflicts=('api-calls')
source=("${pkgname}-${pkgver}.deb::%SOURCE_URL%")
sha256sums=('%SHA256%')

package() {
  cd "${srcdir}"

  # Extract the .deb to a temporary directory
  rm -rf debpkg
  mkdir -p debpkg
  bsdtar -xf "${pkgname}-${pkgver}.deb" -C debpkg

  # Extract the payload to pkgdir
  bsdtar -xf debpkg/data.tar.* -C "${pkgdir}"

  # Install license
  _license_path="$(find "${pkgdir}" -maxdepth 4 -type f -iname 'LICENSE' | head -n1)"

  if [[ -n "${_license_path}" ]]; then
    install -Dm644 "${_license_path}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  fi
}
